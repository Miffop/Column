
#board::0;

!meth Game|Init()
//
	#board::0;
	!for(#i::0;#i<9;#i::#i+1;)
	//
		#board|(#i)::0;
	\\
\\
!meth Game|GetI(#i,#j)
//
	#Result::#board|(#i+#j*3);
\\
!meth Game|SetI(#n,#i,#j)
//
	#board|(#i+#j*3)::#n;
\\
!meth Game|Draw()
//
	!for(#j::0;#j<3;#j::#j+1;)
	//
		@Console|WriteLine("-------");
		@Console|Write("|");
		!for(#i::0;#i<3;#i::#i+1;)
		//
			!if(~~@Game|GetI(#i,#j))
			//
				@Console|Write(" ");
			\\
			!else !if(@Game|GetI(#i,#j)==1)
			//
				@Console|Write("X");
			\\
			!else
			//
				@Console|Write("O");
			\\
			@Console|Write("|");
		\\
		@Console|WriteLine();
	\\
	@Console|WriteLine("-------");
\\
!meth Game|CheckLine(#a,#b,#c)
//
	!if(#a==#b && #b==#c)
	//
		#Result::#a;
	\\
	!else
	//
		#Result::0;
	\\
\\
!meth Game|Check()
//
	!for(#i::0;#i<3;#i::#i+1;)
	//
		#Result::@Game|CheckLine(@Game|GetI(#i,0),
								@Game|GetI(#i,1),
								@Game|GetI(#i,2));
		!if(#Result)
		//
			!break;
		\\
		#Result::@Game|CheckLine(@Game|GetI(0,#i),
								@Game|GetI(1,#i),
								@Game|GetI(2,#i));
		!if(#Result)
		//
			!break;
		\\
	\\
	!if(~~#Result)
	//
		#Result::@Game|CheckLine(@Game|GetI(0,0),
								@Game|GetI(1,1),
								@Game|GetI(2,2));
	\\
	!if(~~#Result)
	//
		#Result::@Game|CheckLine(@Game|GetI(0,2),
								@Game|GetI(1,1),
								@Game|GetI(2,0));
	\\
\\
!meth Game|IsEnd()
//
\\
!meth Game|PlayerTurn()
//

	!repeat
	//
		#res::@Console|ReadLine();
		!if(@String|Length(#res)!=2)
		//
			@Error|Coord();
		\\
		!else
		//
			#res|0::@String|Char(#res,0);
			#res|1::@String|Char(#res,1);
			!if(@Char|IsDigit(#res|0) && @Char|IsDigit(#res|1))
			//
				#res|0::#res|0-'1';
				#res|1::#res|1-'1';
				!if(#res|0>2 .. #res|0<0)
				//
					@Error|Coord();
				\\
				!else !if(#res|1>2 .. #res|1<0)
				//
					@Error|Coord();
				\\
				!else !if(~~@Game|GetI(#res|0,#res|1))
				//
					@Game|SetI(1,#res|0,#res|1);
					!break;
				\\
				!else
				//
					@Error|Placed();
				\\
			\\
			!else
			//
				@Error|Coord();
			\\
		\\
	\\
	!while(1);
\\


!meth Max(#a,#b)
//
	!if(#a>#b)
	//
		#Result::#a;
	\\
	!else
	//
		#Result::#b;
	\\
\\

!meth Game|BotScore(#m,#t)
//

	#Result::0;
	!if(#t!=9)
	//
	
	#Result::_1;
	!for(#i::0;#i<9;#i::#i+1;)
	//
		!if(~~#board|(#i))
		//
			#board|(#i)::#m;
			!if(@Game|Check()==#m)
			//
				#Result::1;
			\\
			!else
			//
				#Result::@Max(_@Game|BotScore(_#m,#t+1),#Result);
			\\
			#board|(#i)::0;
			!if(#Result==1)
			//
				!break;
			\\
		\\
	\\
	\\
\\

!meth Game|BotTurn(#t)
//
	#best::_1;
	#best|S::_2;
	!for(#i::0;#i<9;#i::#i+1;)
	//
		!if(~~#board|(#i))
		//
			#board|(#i)::_1;
			!if(@Game|Check()==_1)
			//
				#best::#i;
				#best|S::1;
			\\
			!else
			//
				#Current::_@Game|BotScore(1,#t+1);
				!if(#Current>=#best|S)
				//
					#best::#i;
					#best|S::#Current;
				\\
			\\
			#board|(#i)::0;
			!if(#best|S==1)
			//
				!break;
			\\
		\\
	\\
	#board|(#best)::_1;
\\

!meth Error|Coord()
//
	@Console|WriteLine("You have to write 2 numbers from 1 to 3");
\\
!meth Error|Placed()
//
	@Console|WriteLine("This place has already been occupied");
\\
!meth Say|Win()
//
	@Console|WriteLine("You Win)");
\\
!meth Say|Lose()
//
	@Console|WriteLine("You Lose(");
\\
!meth Say|Draw()
//
	@Console|WriteLine("Draw");
\\



@Game|Init();
#board|8::_1;
#time::1;
!while(~~#board)
//
	@Console|Clear();
	@Game|Draw();
	@Game|PlayerTurn();
	#time::#time+1;
	#board::@Game|Check();
	!if(#board .. #time==9)
	//
		!break;
	\\
	@Game|BotTurn(#time);
	#time::#time+1;
	#board::@Game|Check();
	!if(#board .. #time==9)
	//
		!break;
	\\
\\

@Console|Clear();
@Game|Draw();
!if(#board==1)
//
	@Say|Win();
\\
!else !if(#board==0)
//
	@Say|Draw();
\\
!else
//
	@Say|Lose();
\\

@Console|ReadKey();